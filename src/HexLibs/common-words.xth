.include hex.xth

// ================ Common Words ================ \\

: drop-all //(many - )
    get-stack-size list-wrap-many drop
;

: caster-raycast-block //( - vector)
	get-caster entity-position
	get-caster entity-lookvector
	raycast-block
;

: caster-raycast-block-face //( - vector)
	get-caster entity-position
	get-caster entity-lookvector
	raycast-block-face
;

: caster-raycast-entity //( - vector)
	get-caster entity-position
	get-caster entity-lookvector
	raycast-entity
;

: ?caster-sneaking //( - bool)
	get-caster entity-height \1.7 ?lt
;

: create-random-unit-vector
	random .pattern north_west aqaawd sub .2 mul
	random .pattern north_west aqaawd sub .2 mul
	random .pattern north_west aqaawd sub .2 mul
	vector-wrap
;

: unit-vector-to-angles //(vector - number(yaw) number(pitch))
	vector-unwrap	//z y x
	rot				//y z x
	swap			//y x z
	atan2			//y yaw
	swap			//yaw y
	asin			//yaw pitch
;

: angles-to-unit-vector //(number(yaw) number(pitch) - vector)
	dup sin			//yaw pitch y
	rrot			//y yaw pitch
	cos				//y yaw xylen
	swap			//y xylen yaw
	2dup			//y xylen yaw xylen yaw
	sin mul			//y xylen yaw z
	rrot			//y z xylen yaw
	cos mul			//y z x
	rot				//z x y
	swap			//z y x
	vector-wrap		//result
;

: radians-to-degrees //(number - number)
	.180 const-pi div mul
;

: degrees-to-radians //(number - number)
	const-pi .180 div mul
;

: get-impetus-facing-entity //(number - entity|null)
	create-vector-pos-y
	mul
	get-impetus-position
	get-impetus-lookvector
	add
	add
	position-entity
;

: get-circle-interior-size //( - vector)
	get-circle-upper-south-east-position
	get-circle-lower-north-west-position
	sub
	.1 .0 .1 vector-wrap
	sub
;

: get-circle-lower-north-west-interior-position //( - vector)
	get-circle-lower-north-west-position
	.1 .0 .1 vector-wrap
	add
;